/*
 * Copyright (c) 2016 deltaDNA Ltd. All rights reserved.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext {
        kotlinVersion = '1.2.31'
        supportVersion = '26.1.0'
        firebaseVersion = '11.4.2'
        
        logTagName  = 'LOG_TAG'
        logTagValue = 'deltaDNA'
    }
    
    repositories {
        jcenter()
        google()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha10'
        classpath('com.dicedmelon.gradle:jacoco-android:0.1.2') {
            // https://github.com/arturdm/jacoco-android-gradle-plugin/issues/42
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
        classpath 'org.jacoco:org.jacoco.core:0.8.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.google.com' }
    }
}

subprojects {
    def isLibrary = it.name.startsWith('library')
    
    if (isLibrary) {
        apply plugin: 'com.android.library'
        apply plugin: 'jacoco-android'
    } else {
        apply plugin: 'com.android.application'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    
    if (isLibrary) {
        // workaround for group/version not picked up through project() dependency
        group = GROUP
        version = VERSION_NAME
    }
    
    android {
        compileSdkVersion 26
        buildToolsVersion '27.0.3'
        
        defaultConfig {
            minSdkVersion 15
            targetSdkVersion 26
            
            versionCode 1
            versionName VERSION_NAME
            
            buildConfigField('String', logTagName, "\"$logTagValue\"")
            
            if (isLibrary) {
                archivesBaseName = "${POM_ARTIFACT_ID}-${versionName}.${System.getenv("BUILD_NUMBER") ?: getSha()}"
                
                consumerProguardFiles 'proguard.cfg'
            }
        }
        
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        
        if (isLibrary) {
            sourceSets {
                test.java.srcDirs += 'src/test/kotlin'
            }
            
            testOptions {
                unitTests {
                    returnDefaultValues = true
                    includeAndroidResources = true
                }
            }
            
            buildTypes {
                debug {
                    testCoverageEnabled true
                }
            }
        }
    }
    
    if (isLibrary) {
        dependencies {
            testImplementation 'com.google.truth:truth:0.37'
            testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
            testImplementation 'com.squareup.okhttp:mockwebserver:2.7.5'
            testImplementation 'junit:junit:4.12'
            testImplementation 'nl.jqno.equalsverifier:equalsverifier:2.3.3'
            testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
            testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
            testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
            testImplementation 'org.json:json:20171018'
            testImplementation 'org.mockito:mockito-core:2.16.0'
            testImplementation 'org.robolectric:robolectric:3.8'
        }
    }
    
    if (isLibrary) {
        apply from: "$rootProject.projectDir/gradle/mvn-push.gradle"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def getSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}
